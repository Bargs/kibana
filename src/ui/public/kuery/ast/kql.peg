// Initialization block
{
  var { nodeTypes: { function: { buildNode }}} = options.helpers;
}

start
  = OrQuery
  / EmptyQuery

EmptyQuery
  = Space* {
    return buildNode('and', []);
  }

OrQuery
  = left:AndQuery Or right:OrQuery {
    return buildNode('or', [left, right]);
  }
  / AndQuery

AndQuery
  = left:NotQuery And right:AndQuery{
    return buildNode('and', [left, right]);
  }
  / NotQuery

NotQuery
  = Not query:SubQuery {
    return buildNode('not', query);
  }
  / SubQuery

SubQuery
  = '(' Space* query:OrQuery Space* ')' { return query; }
  / Expression

Expression
  = FieldRangeExpression
  / FieldValueExpression
  / ValueExpression

FieldRangeExpression
  = field:Literal Space* operator:RangeOperator Space* value:Literal {
    return buildNode('range', field, { [operator]: value });
  }

FieldValueExpression
  = field:Literal Space* ':' Space* partial:ListOfValues {
    return partial(field);
  }

ValueExpression
  = partial:Value {
    const field = null;
    return partial(field);
  }

ListOfValues
  = '(' Space* partial:OrListOfValues Space* ')' { return partial; }
  / Value

OrListOfValues
  = partialLeft:AndListOfValues Or partialRight:OrListOfValues {
    return (field) => buildNode('or', [partialLeft(field), partialRight(field)]);
  }
  / AndListOfValues

AndListOfValues
  = partialLeft:NotListOfValues And partialRight:AndListOfValues {
    return (field) => buildNode('and', [partialLeft(field), partialRight(field)]);
  }
  / NotListOfValues

NotListOfValues
  = Not partial:ListOfValues {
    return (field) => buildNode('not', partial(field));
  }
  / ListOfValues

Value
  = value:Literal {
    return (field) => buildNode('is', field, value);
  }

Or
  = Space+ 'or'i Space+

And
  = Space+ 'and'i Space+

Not
  = 'not'i Space+

Literal
  = QuotedString
  / UnquotedLiteral

QuotedString
  = '"' chars:(EscapedSpecialCharacter / [^"])* '"' {
    return chars.join('');
  }

UnquotedLiteral
  = chars:UnquotedCharacter+ {
    const sequence = chars.join('').trim();
    if (sequence === 'null') return null;
    if (sequence === 'true') return true;
    if (sequence === 'false') return false;
    const number = Number(sequence);
    return isNaN(number) ? sequence : number;
  }

UnquotedCharacter
  = EscapedSpecialCharacter
  / !Separator char:. { return char; }

EscapedSpecialCharacter
  = '\\' char:SpecialCharacter { return char; }

Separator
  = Keyword / SpecialCharacter

Keyword
  = Or / And / Not

SpecialCharacter
  = [():<>"]

RangeOperator
  = '<=' { return 'lte'; }
  / '>=' { return 'gte'; }
  / '<' { return 'lt'; }
  / '>' { return 'gt'; }

Space
  = [\ \t\r\n]
